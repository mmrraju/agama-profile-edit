Flow org.gluu.agama.profile.edit
     Basepath ""
     Timeout 6000 seconds
// Profile edit flow starting
Log "@info Profile edit flow starting..."
// Create  variable for UI feedback
uiFeedback = {}
emailObj = {}
// Iterate x times max
Repeat 6 times max
     // Retrieve user mail  from UI
     userInput = RRF "mailform.ftlh" emailObj
     // create an instance of user profile update service
     userProfileEditService = Call org.gluu.agama.profile.service.UserProfileEditService#getInstance 
     // Check user already exist or not with mail
     user = Call userProfileEditService getUserEntity userInput.mail
     // If user found
     When user.empty is not true
          // User found with e-mail
          Log "@info User found with e-mail % " userInput.mail
          // OTP to verify user
          otp = Call userProfileEditService sendMail userInput.mail
          // OTP form feedback
          otpFormFeedback = {}
          // Max 3 times repeat
          Repeat 3 times max
               // OTP Form
               otpFormData = RRF "otp.ftlh" otpFormFeedback
               // If OTP match
               When otpFormData.passcode is otp
                    // OTP matches
                    Log "@info OTP matches"
                    // Profile Update Feedback
                    profileUpdateFeedback = {}
                    // Max 3 times repeat
                    Repeat 3 times max
                         // Profile update form
                         userProfiles = RRF "profile.ftlh" profileUpdateFeedback
                         // Validate user profile inputs
                         validation = Call userProfileEditService validateInputs userProfiles
                         // Validation result log
                         Log "@info Validation result:  % , % , %" validation validation.valid validation.message
                         // If successfully validate
                         When validation.valid is true
                              // Update user profile
                              updateProfile | E = Call userProfileEditService updateProfile userProfiles userInput.mail
                              // Profile update success or not
                              When updateProfile.empty is not true
                                   // Show acknowledgement UI page for successful update
                                   ack = RRF "acknowledgement.ftlh" 
                                   // Finish successfully
                                   Finish user.uid
                              Otherwise
                                   // Update error
                                   profileUpdateFeedback = E
                         Otherwise
                              // Validate message
                              profileUpdateFeedback.errorMessage = validation.message
               Otherwise
                    // OTP doesn't match
                    otpFormFeedback.errorMessage = "OTP doesn't match"
     Otherwise
          // User not found
          emailObj.errorMessage = "User not found"
// Maximum attempt reached
Log "@info Maximum attempt reached"